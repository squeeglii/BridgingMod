import net.darkhax.curseforgegradle.TaskPublishCurseForge

apply plugin: "com.github.johnrengelman.shadow"
apply plugin: "com.modrinth.minotaur"
apply plugin: "net.darkhax.curseforgegradle"

apply from: "$rootDir/publishing.gradle"

architectury {
    platformSetupLoomIde()
    neoForge()
}

loom {
    accessWidenerPath = project(":common").loom.accessWidenerPath
}


configurations {
    common
    shadowCommon // Don't use shadow from the shadow plugin since it *excludes* files.
    compileOnly.extendsFrom common
    runtimeOnly.extendsFrom common
    developmentNeoForge.extendsFrom common
}

repositories {
    maven {
        url "https://maven.neoforged.net/releases/"
    }
    maven {
        name = 'Kotlin for Forge'
        url = 'https://thedarkcolour.github.io/KotlinForForge/'
        content { includeGroup "thedarkcolour" }
    }
}

dependencies {
    neoForge "net.neoforged:neoforge:${rootProject.neoforge_version}"

    modImplementation "dev.isxander:yet-another-config-lib:${project.yacl_version}-neoforge"

    modCompileOnly(fileTree(dir: 'jarDeps', include: '*.jar'))
    modCompileOnly("maven.modrinth:dynamiccrosshair:${rootProject.dynamiccrosshair_version_neoforge}")

    common(project(path: ":common", configuration: "namedElements")) { transitive false }
    shadowCommon(project(path: ":common", configuration: "transformProductionNeoForge")) { transitive = false }
}

shadowJar {
    exclude "fabric.mod.json"
    exclude "architectury.common.json"

    configurations = [ project.configurations.shadowCommon ]
    archiveClassifier.set("dev-shadow")
}

remapJar {
    inputFile.set shadowJar.archiveFile
    dependsOn shadowJar
    archiveClassifier.set("release")
}

jar {
    archiveClassifier.set("dev")
}

processResources {

    // 1.20.5 support - this'll probably be covered by architectury soon enough.
    rename "mods.toml", "neoforge.mods.toml"
}

sourcesJar {
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}


modrinth {
    String[] versions = getSupportedVersions(project.properties);

    token = getModrinthToken(project)
    projectId = "bridging-mod"
    versionName = "Bridging Mod ${formattedBaseVersion} (NeoForge)"
    versionNumber = project.version.toString()
    versionType = project.properties["release_type"].toString()
    uploadFile = remapJar
    gameVersions = Arrays.asList(versions)
    loaders = [ "neoforge" ]

    changelog = new File(project.getRootDir(), "changelog.md").readLines().join("\n")

    var shouldPublish = project.properties["debug_publishing"]
    if(shouldPublish != null && shouldPublish.toString().equalsIgnoreCase("true"))
        setDebugMode(true)

    dependencies {
        required.project "cloth-config"
    }
}

tasks.register('curseforge', TaskPublishCurseForge) {
    group = "publishing"
    description = "Publishes project to CurseForge"

    String mcVer = project.properties["minecraft_version"]
    String projectVer = project.properties["mod_version"]
    String projectId = project.properties["curseforge_project"]

    if(projectVer == null || projectVer.isBlank())
        throw new IllegalStateException("Project version should not be null when publishing.")

    apiToken = getCurseToken(project)

    var shouldPublish = project.properties["debug_publishing"]
    if(shouldPublish != null && shouldPublish.toString().equalsIgnoreCase("true"))
        debugMode = true

    def mainFile = upload(projectId, remapJar)
    mainFile.displayName = "[NeoForge %s] v%s Bridging Mod".formatted(mcVer, projectVer)
    mainFile.addModLoader("neoforge")
    mainFile.releaseType = project.properties["release_type"]

    mainFile.changelogType = "markdown"
    mainFile.changelog = new File(project.getRootDir(), "changelog.md")

    for(String version: getSupportedVersions(project.properties))
        mainFile.addGameVersion(version)

    mainFile.addRequirement("cloth-config")

}