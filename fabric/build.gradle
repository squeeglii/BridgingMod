import net.darkhax.curseforgegradle.TaskPublishCurseForge

apply plugin: "com.github.johnrengelman.shadow"
apply plugin: "com.modrinth.minotaur"
apply plugin: "net.darkhax.curseforgegradle"

apply from: "$rootDir/publishing.gradle"

architectury {
    platformSetupLoomIde()
    fabric()
}

loom {
    accessWidenerPath = project(":common").loom.accessWidenerPath
}

configurations {
    common
    shadowCommon // Don't use shadow from the shadow plugin since it *excludes* files.
    compileOnly.extendsFrom common
    runtimeOnly.extendsFrom common
    developmentFabric.extendsFrom common
}

dependencies {
    modImplementation "net.fabricmc:fabric-loader:${rootProject.fabric_loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${rootProject.fabric_version}"

    modApi("me.shedaniel.cloth:cloth-config-fabric:${rootProject.clothconfig_version}") {
        exclude(group: "net.fabricmc.fabric-api")
    }

    modApi("com.terraformersmc:modmenu:${rootProject.modmenu_version}")
    modCompileOnly("maven.modrinth:dynamiccrosshair:${rootProject.dynamiccrosshair_version_fabric}")

    common(project(path: ":common", configuration: "namedElements")) { transitive false }
    shadowCommon(project(path: ":common", configuration: "transformProductionFabric")) { transitive false }
}

shadowJar {
    exclude "architectury.common.json"

    configurations = [project.configurations.shadowCommon]
    archiveClassifier.set("dev-shadow")
}

remapJar {
    injectAccessWidener = true
    inputFile.set shadowJar.archiveFile
    dependsOn shadowJar
    archiveClassifier.set("release")
}

jar {
    archiveClassifier.set("dev")
}

sourcesJar {
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}

modrinth {
    String[] versions = getSupportedVersions(project.properties);

    token = getModrinthToken(project)
    projectId = "bridging-mod"
    versionName = "Bridging Mod ${formattedBaseVersion} (Fabric)"
    versionNumber = project.version.toString()
    versionType = project.properties["release_type"].toString()
    uploadFile = remapJar
    gameVersions = Arrays.asList(versions)
    loaders = [ "fabric", "quilt" ]

    changelog = new File(project.getRootDir(), "changelog.md").readLines().join("\n")

    var shouldPublish = project.properties["debug_publishing"]
    if(shouldPublish != null && shouldPublish.toString().equalsIgnoreCase("true"))
        setDebugMode(true)

    dependencies {
        required.project "fabric-api"
        required.project "cloth-config"
        optional.project "modmenu"
    }
}

tasks.register('curseforge', TaskPublishCurseForge) {
    group = "publishing"
    description = "Publishes project to CurseForge"

    String[] minecraftVersions = getSupportedVersions(project.properties)
    String mcVer = minecraftVersions.size() > 0
            ? minecraftVersions[0]
            : "any"
    String projectVer = project.properties["mod_version"]
    String projectId = project.properties["curseforge_project"]

    if(projectVer == null || projectVer.isBlank())
        throw new IllegalStateException("Project version should not be null when publishing.")

    apiToken = getCurseToken(project)

    var shouldPublish = project.properties["debug_publishing"]
    if(shouldPublish != null && shouldPublish.toString().equalsIgnoreCase("true"))
        debugMode = true

    def mainFile = upload(projectId, remapJar)
    mainFile.displayName = "[Fabric %s] v%s Bridging Mod".formatted(mcVer, projectVer)
    mainFile.addModLoader("fabric")
    mainFile.releaseType = project.properties["release_type"]

    mainFile.changelogType = "markdown"
    mainFile.changelog = new File(project.getRootDir(), "changelog.md")

    for(String version: getSupportedVersions(project.properties))
        mainFile.addGameVersion(version)

    mainFile.addRequirement("fabric-api")
    mainFile.addRequirement("cloth-config")

}